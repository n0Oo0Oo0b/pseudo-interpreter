blocks          = procDecl
                | functionDecl
                | ifStmt
                | caseStmt
                | statement ;

procDecl        = "PROCEDURE" IDENTIFIER "(" parameters? ")"
                    statement* "ENDPROCEDURE" ;
functionDecl    = "FUNCTION" IDENTIFIER "(" parameters? ")" "RETURNS" datatype
                    statement* "ENDFUNCTION";
ifStmt          = "IF" expression
                    "THEN" statement*
                    ("ELSE" statement*)?
                    "ENDIF"
caseStmt        = "CASE" "OF" IDENTIFIER
                    (literal ":" statement)+
                    ("OTHERWISE" statement)?
                    "ENDCASE"


statement       = declareStmt
                | constantDecl
                | inputStmt
                | outputStmt
                | returnStmt
                | fileOp
                | exprStmt ;

declareStmt     = "DECLARE" IDENTIFIER ":" datatype ;
constantDecl    = "CONSTANT" IDENTIFIER "<-" literal ;
inputStmt       = "INPUT" IDENTIFIER ;
outputStmt      = "OUTPUT" arguments ;
returnStmt      = "RETURN" expression ;
fileOp          = fOpenStmt
                | fReadStmt
                | fWriteStmt
                | fCloseStmt
fOpenStmt       = "OPENFILE" fileIdentifier "FOR" fileMode ;
fReadStmt       = "READFILE" fileIdentifier "," IDENTIFIER ;
fWriteStmt      = "WRITEFILE" fileIdentifier "," IDENTIFIER ;
fCloseStmt      = "CLOSEFILE" fileIdentifier ;
exprStmt        = expression ;



expression      = assignment ;

assignment      = IDENTIFIER () "<-" logicOr
                | logicOr ;
logicOr         = logicAnd ( "OR" logicAnd )* ;
logicAnd        = logicNot ( "AND" logicNot )* ;
logicNot        = "NOT"? logicNot | comparison ;
comparison      = term ( ( "<>" | "=" | "<=" | ">=" | "<" | ">") term )* ;
term            = factor ( ( "-" | "+" ) factor )* ;
factor          = call ( ( "/" | "*" ) call )* ;
call            = primary ( "(" arguments? ")" | "[" arguments "]" )* ;
primary         = literal | IDENTIFIER | "(" expression ")" ;



datatype        = basicType | arrayType ;
arrayType       = "ARRAY" "[" arrayRange ("," arrayRange)* "]" "OF" basicType ;
arrayRange      = expression ":" expression ;
basicType       = "INTEGER" | "REAL" | "CHAR" | "STRING" | "BOOLEAN" ;
literal         = INTEGER | REAL | CHAR | STRING | BOOLEAN ;
arguments       = expression ("," expression)* ;
parameters      = IDENTIFIER ":" datatype ("," IDENTIFIER ":" datatype)* ;

fileIdentifier  = STRING ;
fileMode        = "READ" | "WRITE" ;



INTEGER         = "-"? DIGIT+ ;
REAL            = "-"? DIGIT+ "." DIGIT+ ;
CHAR            = "'" <any char except "'"> "'" ;
STRING          = "\"" <any char except "\"">* "\"" ;
BOOLEAN         = "TRUE" | "FALSE" ;
IDENTIFIER      = ALPHA ( ALPHA | DIGIT )* ;
ALPHA           = "a" ... "z" | "A" ... "Z" ;
DIGIT           = "0" ... "9" ;
NEWLINE         = "\n" ;
